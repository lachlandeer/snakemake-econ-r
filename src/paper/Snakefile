# subworkflow: paper
#
# rticle-style: asa
#
# contributors: @lachlandeer, @julianlanger

import glob

# --- Importing Configuration Files --- #
configfile: "../../" + "paths.yaml"

ROOT = config["sub2root"]
HERE = ROOT + config["src_paper"]
LIB  = ROOT + config["src_lib"]
OUT  = config["out"]

# --- Sub Workflow Dependencies --- #
subworkflow figures:
    workdir:  ROOT + config["ROOT"]
    snakefile: ROOT + config["src_figures"] + "Snakefile"

subworkflow tables:
    workdir:   ROOT + config["ROOT"]
    snakefile: ROOT + config["src_tables"] + "Snakefile"

# --- Dictionaries --- #
RMD_FILES  = glob.glob("*.Rmd")
YAML_FILES = glob.glob("*.yml")
BIB_FILES  = glob.glob("*.bib")
TEX_FILES  = glob.glob("*.tex")

# --- Variable Declarations ---- #
runR = "Rscript --no-save --no-restore --verbose"
logAll = "2>&1"

# --- Build Rules --- #

## paper2root:   copy paper to root directory
rule paper2root:
    input:
        pdf  = ROOT + config["out_paper"] + "my_article.pdf",
    output:
        pdf  = ROOT + PROJ_NAME + ".pdf",
    shell:
        "cp {input.pdf} {output.pdf}"

## knit_pdf: builds pdf using bookdown
rule knit_pdf:
    input:
        text_files = RMD_FILES,
        yaml_files = YAML_FILES,
        biblo      = BIB_FILES,
        tex_style  = TEX_FILES,
        tables     = tables(expand(config["out_tables"] +
                            "{iTable}.tex",
                            iTable = TABLES)),
        figures = figures(expand(config["out_figures"] +
                            "{iPlot}.pdf",
                            iPlot = PLOTS)),
        runner     = LIB + "build_pdfbook.R"
    output:
        ROOT + config["out_paper"] + "my_article.pdf"
    log:
        ROOT + config["log"] + "paper/build_pdf.Rout"
    shell:
        "{runR} {input.runner} > {log} {logAll}"


rule clean:
    shell:
        "rm -rf _book/* bookdown_files/* *.pdf *.html"
